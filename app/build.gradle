
buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath rootProject.ext.ClassPaths.kotlin
    }
}

def config = rootProject.ext
def plugins = config.Plugins
def libs = config.Libraries

apply plugin: plugins.android_app
apply plugin: plugins.kotlin
apply plugin: plugins.kotlin_kapt

apply from: "$rootDir/config/config.gradle", to: config

android {
    def versions = config.Versions
    def appConfig = config.AppConfig

    compileSdkVersion versions.compileSdk

    defaultConfig {
        minSdkVersion versions.minSdk
        targetSdkVersion versions.targetSdk

        applicationId appConfig.id
        versionCode appConfig.versionCode
        versionName appConfig.versionName

        vectorDrawables.useSupportLibrary = true
    }

    signingConfigs {
        release_keystore {
            storeFile file(config.keystore_file)
            storePassword config.keystore_password
            keyAlias config.keystore_key
            keyPassword config.key_password
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release_keystore
        }
        debug {
            applicationIdSuffix appConfig.debugSuffix
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    packagingOptions {
        jniLibs {
            excludes += ['META-INF/LICENSE*']
        }
        resources {
            excludes += ['META-INF/LICENSE*', 'META-INF/atomicfu.kotlin_module']
        }
    }


    kotlinOptions {
        jvmTarget = '1.8'
    }

    buildFeatures {
        viewBinding true
    }
    lint {
        abortOnError false
        htmlReport true
        warningsAsErrors true
    }

}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation libs.kotlin

    implementation libs.lifecycle_runtime

    // Android Support
    implementation libs.androidx_core
    implementation libs.androidx_appCompat
    implementation libs.androidx_annotations
    implementation libs.androidx_constraintLayout

    implementation libs.timber

    implementation libs.koin_core
    implementation libs.koin_android
}
